BASE <https://id.volunteeringdata.io/>
PREFIX : <https://id.volunteeringdata.io/schema/>
PREFIX id: <https://id.volunteeringdata.io/>

CONSTRUCT {
    ?organisation
        a :Organisation ;
        :label ?organisationLabel ;
        :activity ?organisationActivity ;
        :description ?organisationDescription ;
        :purpose ?organisationPurpose ;
        :website ?organisationWebsite ;
        :location ?organisationLocation ;
        :email ?organisationEmail ;
        :phone ?organisationPhone ;
        :cause ?organisationCause ;
    .

    ?organisationLocation
        :locationType ?locationType ;
        :address ?address ;
        :latitude ?latitude ;
        :longitude ?longitude ;
    .

    ?organisationCause :label ?causeLabel .
}
WHERE {
    FILTER(?organisation = IRI($id))
    ?organisation
        a :Organisation ;
        :label ?organisationLabel ;
        :activity ?organisationActivity ;
    .

    OPTIONAL { ?organisation :description ?organisationDescription . }
    OPTIONAL { ?organisation :purpose ?organisationPurpose . }
    OPTIONAL { ?organisation :website ?organisationWebsite . }
    OPTIONAL { ?organisation :email ?organisationEmail . }
    OPTIONAL { ?organisation :phone ?organisationPhone . }
    OPTIONAL {
        ?organisation :location ?organisationLocation .
        OPTIONAL { ?organisationLocation :locationType ?locationType . }
        OPTIONAL { ?organisationLocation :address ?address . }
        OPTIONAL { ?organisationLocation :latitude ?latitude . }
        OPTIONAL { ?organisationLocation :longitude ?longitude . }
    }
    OPTIONAL {
        ?organisation :cause ?organisationCause .
        ?organisationCause :label ?causeLabel .
    }
}
