BASE <https://id.volunteeringdata.io/>
PREFIX : <https://id.volunteeringdata.io/schema/>

CONSTRUCT {
    <Organisations> :organisations ?organisation .

    ?organisation
        a :Organisation ;
        :organisationLabel ?organisationLabel ;
        :organisationDescription ?organisationDescription ;
        :organisationEmail ?organisationEmail ;
        :organisationLocation ?organisationLocation ;
        :organisationPurpose ?organisationPurpose ;
        :organisationPhone ?organisationPhone ;
        :organisationLogo ?organisationLogo ;
        :organisationCause ?organisationCause ;
    .

    ?organisationLocation
        :locationType ?locationType ;
        :address ?address ;
        :latitude ?latitude ;
        :longitude ?longitude ;
    .

    ?organisationCause :label ?causeLabel .
}
WHERE {
    ?organisation
        a :Organisation ;
        :organisationLabel ?organisationLabel ;
    .

    OPTIONAL { ?organisation :organisationDescription ?organisationDescription . }
    OPTIONAL { ?organisation :organisationEmail ?organisationEmail . }
    OPTIONAL { ?organisation :organisationPurpose ?organisationPurpose . }
    OPTIONAL { ?organisation :organisationPhone ?organisationPhone . }
    OPTIONAL { ?organisation :organisationLogo ?organisationLogo . }
    OPTIONAL {
        ?organisation :organisationLocation ?organisationLocation .
        OPTIONAL { ?organisationLocation :locationType ?locationType . }
        OPTIONAL { ?organisationLocation :address ?address . }
        OPTIONAL { ?organisationLocation :latitude ?latitude . }
        OPTIONAL { ?organisationLocation :longitude ?longitude . }
    }
    OPTIONAL {
        ?organisation :organisationCause ?organisationCause .
        ?organisationCause :label ?causeLabel .
    }
}
