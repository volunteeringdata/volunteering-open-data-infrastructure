BASE <https://id.volunteeringdata.io/>
PREFIX : <https://id.volunteeringdata.io/schema/>

CONSTRUCT {
    <Organisations> :organisations ?organisation .

    ?organisation
        a :Organisation ;
        :label ?organisationLabel ;
        :activity ?organisationActivity ;
        :description ?organisationDescription ;
        :purpose ?organisationPurpose ;
        :url ?organisationUrl ;
        :location ?organisationLocation ;
        :contact ?organisationContact ;
        :cause ?organisationCause ;
    .

    ?organisationLocation
        :locationType ?locationType ;
        :address ?address ;
        :latitude ?latitude ;
        :longitude ?longitude ;
    .

    ?organisationContact
        :label ?contactLabel ;
        :email ?contactEmail ;
        :phone ?contactPhone ;
    .

    ?organisationCause :label ?causeLabel .
}
WHERE {
    ?organisation
        a :Organisation ;
        :label ?organisationLabel ;
        :activity ?organisationActivity ;
    .

    OPTIONAL { ?organisation :description ?organisationDescription . }
    OPTIONAL { ?organisation :purpose ?organisationPurpose . }
    OPTIONAL { ?organisation :url ?organisationUrl . }
    OPTIONAL {
        ?organisation :location ?organisationLocation .
        OPTIONAL { ?organisationLocation :locationType ?locationType . }
        OPTIONAL { ?organisationLocation :address ?address . }
        OPTIONAL { ?organisationLocation :latitude ?latitude . }
        OPTIONAL { ?organisationLocation :longitude ?longitude . }
    }
    OPTIONAL {
        ?organisation :contact ?organisationContact .
        OPTIONAL { ?organisationContact :label ?contactLabel . }
        OPTIONAL { ?organisationContact :email ?contactEmail . }
        OPTIONAL { ?organisationContact :phone ?contactPhone . }
    }
    OPTIONAL {
        ?organisation :cause ?organisationCause .
        ?organisationCause :label ?causeLabel .
    }
}
