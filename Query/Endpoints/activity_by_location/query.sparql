BASE <https://id.volunteeringdata.io/>
PREFIX : <https://id.volunteeringdata.io/schema/>
PREFIX units: <http://www.opengis.net/def/uom/OGC/1.0/>
PREFIX spatialF: <http://jena.apache.org/function/spatial#>

SELECT ?name ?distance {
    ?activity
        :activityLocation ?location ;
        :activityLabel ?name ;
    .

    ?location 
        :latitude ?orgLat ;
        :longitude ?orgLon ;
    .
        
    BIND(spatialF:convertLatLon(?orgLat, ?orgLon) as ?orgLocation)
    BIND(spatialF:convertLatLon($lat, $lon) as ?myLocation)

    FILTER(spatialF:nearby(?orgLocation, ?myLocation, $within, units:kilometer))

    BIND(spatialF:distance(?orgLocation, ?myLocation, units:kilometer) as ?distance)
}
ORDER BY ?distance
