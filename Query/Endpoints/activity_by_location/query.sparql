BASE                    <https://id.volunteeringdata.io/>

PREFIX id:              <https://id.volunteeringdata.io/>
PREFIX :                <https://id.volunteeringdata.io/schema/>

PREFIX units:           <http://www.opengis.net/def/uom/OGC/1.0/>
PREFIX spatialF:        <http://jena.apache.org/function/spatial#>
prefix xsd:             <http://www.w3.org/2001/XMLSchema#>

SELECT ?activity_id ?activity_name ?activity_latitude ?activity_longitude ?activity_description ?organisation_name ?organisation_description (xsd:integer(?activity_distance_from_search_location) as ?activity_distance_from_search_location_in_metres) {
    ?session
        :sessionLocation ?location ;
    .

    ?location 
        :latitude ?activity_latitude ;
        :longitude ?activity_longitude ;
    .

    BIND(spatialF:convertLatLon(?activity_latitude, ?activity_longitude) as ?activity_location)
    BIND(spatialF:convertLatLon($lat, $lon) as ?search_location)

    FILTER(spatialF:nearby(?activity_location, ?search_location, $within, units:metre))

    BIND(spatialF:distance(?activity_location, ?search_location, units:metre) as ?activity_distance_from_search_location)

    ?activity_id
        :activitySession ?session ;
        :label ?activity_name ;
        :activityDescription ?activity_description ;
        :activityOrganisation/:label ?organisation_name ;
        :activityOrganisation/:organisationDescription ?organisation_description ;
    .
}
ORDER BY ?activity_distance_from_search_location
