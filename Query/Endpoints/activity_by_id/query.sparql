BASE <https://id.volunteeringdata.io/>
PREFIX : <https://id.volunteeringdata.io/schema/>
PREFIX id: <https://id.volunteeringdata.io/>

CONSTRUCT {
    ?activity
        a :Activity ;
        :label ?activityLabel ;
        :description ?activityDescription ;
        :applyLink ?activityApplyLink ;
        :email ?activityEmail ;
        :phone ?activityPhone ;
        :session ?activitySession ;
        :organisation ?activityOrganisation ;
        :cause ?activityCause ;
    .

    ?activitySession
        :sessionApplyLink ?activitySessionApplyLink ;
        :sessionDue ?activitySessionDue ;
        :sessionEnd ?activitySessionEnd ;
    .

    ?activityOrganisation :label ?activityOrganisationLabel .

    ?activityLocation
        :locationType ?locationType ;
        :address ?address ;
        :latitude ?latitude ;
        :longitude ?longitude ;
    .

    ?activityCause :label ?causeLabel .
}
WHERE {
    FILTER(?activity = IRI($id))
    ?activity
        a :Activity ;
        :label ?activityLabel ;
        :organisation ?activityOrganisation ;
    .

    ?activityOrganisation :label ?activityOrganisationLabel .

    OPTIONAL { ?activity :description ?activityDescription . }
    OPTIONAL { ?activity :applyLink ?activityApplyLink . }
    OPTIONAL { ?activity :email ?activityEmail . }
    OPTIONAL { ?activity :phone ?activityPhone . }
    OPTIONAL {
        ?activity :location ?activityLocation .
        OPTIONAL { ?activityLocation :locationType ?locationType . }
        OPTIONAL { ?activityLocation :address ?address . }
        OPTIONAL { ?activityLocation :latitude ?latitude . }
        OPTIONAL { ?activityLocation :longitude ?longitude . }
    }
    OPTIONAL {
        ?activity :session ?activitySession .
        OPTIONAL { ?activitySession :sessionApplyLink ?activitySessionApplyLink . }
        OPTIONAL { ?activitySession :sessionDue ?activitySessionDue . }
        OPTIONAL { ?activitySession :sessionEnd ?activitySessionEnd . }

    }
    OPTIONAL {
        ?activity :cause ?activityCause .
        ?activityCause :label ?causeLabel .
    }
}
