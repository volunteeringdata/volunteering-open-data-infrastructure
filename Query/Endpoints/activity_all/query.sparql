BASE <https://id.volunteeringdata.io/>
PREFIX : <https://id.volunteeringdata.io/schema/>
PREFIX id: <https://id.volunteeringdata.io/>

CONSTRUCT {
    <Activities> :activities ?activity .

    ?activity
        a :Activity ;
        :label ?activityLabel ;
        :description ?activityDescription ;
        :applyLink ?activityApplyLink ;
        :email ?activityEmail ;
        :phone ?activityPhone ;
        :session ?activitySession ;
        :organisation ?activityOrganisation ;
        :cause ?activityCause ;
    .

    ?activitySession
        :sessionApplyLink ?activitySessionApplyLink ;
        :sessionDue ?activitySessionDue ;
        :sessionEnd ?activitySessionEnd ;
        :location ?activitySessionLocation ;
    .

    ?activityOrganisation :label ?activityOrganisationLabel .

    ?activitySessionLocation
        :locationType ?locationType ;
        :address ?address ;
        :latitude ?latitude ;
        :longitude ?longitude ;
    .

    ?activityCause :label ?causeLabel .
}
WHERE {
    ?activity
        a :Activity ;
        :label ?activityLabel ;
        :organisation ?activityOrganisation ;
    .

    ?activityOrganisation :label ?activityOrganisationLabel .

    OPTIONAL { ?activity :description ?activityDescription . }
    OPTIONAL { ?activity :applyLink ?activityApplyLink . }
    OPTIONAL { ?activity :email ?activityEmail . }
    OPTIONAL { ?activity :phone ?activityPhone . }
    OPTIONAL {
        ?activity :session ?activitySession .
        OPTIONAL { ?activitySession :sessionApplyLink ?activitySessionApplyLink . }
        OPTIONAL { ?activitySession :sessionDue ?activitySessionDue . }
        OPTIONAL { ?activitySession :sessionEnd ?activitySessionEnd . }
        OPTIONAL {
            ?activitySession :location ?activitySessionLocation .
            OPTIONAL { ?activitySessionLocation :locationType ?locationType . }
            OPTIONAL { ?activitySessionLocation :address ?address . }
            OPTIONAL { ?activitySessionLocation :latitude ?latitude . }
            OPTIONAL { ?activitySessionLocation :longitude ?longitude . }
        }
    }
    OPTIONAL {
        ?activity :cause ?activityCause .
        ?activityCause :label ?causeLabel .
    }
}
